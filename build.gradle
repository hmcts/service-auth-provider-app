plugins {
  id 'java'
  id 'application'
  id 'jacoco'
  id 'com.github.ben-manes.versions' version '0.38.0'
  id 'org.owasp.dependencycheck' version '6.2.0'
  id 'org.sonarqube' version '3.2.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.4.5'
  id 'au.com.dius.pact' version '4.2.6'
}

group = 'uk.gov.hmcts.auth.provider.service'

sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
  }
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

repositories {
  jcenter()
  mavenLocal()
  maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
  suppressionFile = 'dependency-check-suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
      }

      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

dependencyManagement {
  dependencies {

    //CVE-2021-22112
    dependencySet(group: 'org.springframework.security', version: '5.5.0') {
      entry 'spring-security-crypto'
    }
  }
}
sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Service Auth Provider"
    property 'sonar.coverage.exclusions', "**/config/**"
  }
}

def versions = [
  springBoot: plugins.getPlugin('org.springframework.boot').class.package.implementationVersion,
  springfoxSwagger: '2.9.2',
  reformLogging: '5.1.7',
  pact_version: '4.1.7',
  junit_jupiter: '5.7.1'
]

ext["rest-assured.version"] = '4.3.0'

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group == 'org.codehaus.groovy') {
        details.useVersion "3.0.2"
        details.because "needed by rest-assured>=4.3"
      }
      if (details.requested.group in ['org.apache.tomcat.embed']) {
        details.useVersion '9.0.44'
      }
    }
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot
  compile group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
  compile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
  compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
  compile group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
  compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
  compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging

  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
  compile group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.14.1'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'

  compileOnly group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'

  testCompile group: 'junit', name: 'junit', version: '4.13.2'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.20.0'

  functionalTestCompile sourceSets.main.runtimeClasspath
  functionalTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  functionalTestCompile group: 'com.typesafe', name: 'config', version: '1.4.1'
  functionalTestCompile group: 'io.rest-assured', name: 'rest-assured'
  functionalTestCompile group: 'junit', name: 'junit', version: '4.13.2'

  contractTestCompile group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
  contractTestCompile group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
  contractTestCompile group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version
  contractTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  contractTestCompile group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
  contractTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit_jupiter
  contractTestRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit_jupiter
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit_jupiter
  contractTestRuntime group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.7.2'

  testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
    exclude(module: 'commons-logging')
  }
}

mainClassName = 'uk.gov.hmcts.auth.provider.service.api.ServiceAuthProviderApplication'

installDist {
  applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]
}

run {
  applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]
}

bootJar {
  archiveName = 'service-auth-provider.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

compileFunctionalTestJava {
  options.compilerArgs << "-Xlint:unchecked"
}

task functional(type: Test, group: 'Verification') {
  description = "Runs Functional Tests"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test, group: 'Verification') {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task contractTest(type: Test) {
  logger.lifecycle("Runnning contract Tests")
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath

}

task runProviderPactVerification(type: Test) {
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runProviderPactVerification.dependsOn contractTest

runProviderPactVerification.finalizedBy pactVerify


jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled true
    html.enabled true
    html.destination file("${buildDir}/reports/jacoco")
    csv.enabled false
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

